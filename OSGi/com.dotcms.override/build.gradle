plugins {
    id 'biz.aQute.bnd.builder' version '3.3.0'
}

sourceCompatibility = '1.8'
version = '0.1'

repositories {
    maven { url "http://repo.dotcms.com/artifactory/libs-release" }
}

configurations {
    osgiLibs
}

dependencies {
    compile('com.dotcms:dotcms:4.3.0') { transitive = true }
}

import java.util.jar.*

ext {
    bundleSymbolicName = ""
    bundleVersion = ""
    importPackage = ""
    bundleManifestVersion = ""
    bundleDocURL = ""
    bundleVendor = ""
}

/////////////////////////
//Plugin jar
/////////////////////////
jar {
    manifest {
        attributes (
                'Bundle-Vendor': 'dotCMS',
                'Bundle-Description': 'dotCMS - Override class example',
                'Bundle-DocURL': 'https://dotcms.com/',
                'Bundle-Activator': 'com.dotmarketing.osgi.override.Activator',
                'Override-Classes': 'com.dotmarketing.portlets.folders.model.Folder',
                'Import-Package': '!com.dotmarketing.portlets.folders.model,*;version=0'
        )
    }
}
jar.finalizedBy 'fragmentJar'

/////////////////////////
//Fragment jar
/////////////////////////

/**
 * Reads the Manifest file of the just created plugin jar in order to get the required info
 * to automatically create the fragment jar.
 */
task readManifesttAttributes {
    doFirst {
        File file = configurations.baseline.singleFile
        JarFile jar = new JarFile(file)
        Attributes manifest = jar.getManifest().getMainAttributes()
        bundleSymbolicName = "${manifest.getValue('Bundle-SymbolicName')}"
        bundleVersion = "${manifest.getValue('Bundle-Version')}"
        importPackage = "${manifest.getValue('Import-Package')}"
        bundleManifestVersion = "${manifest.getValue('Bundle-ManifestVersion')}"
        bundleDocURL = "${manifest.getValue('Bundle-DocURL')}"
        bundleVendor = "${manifest.getValue('Bundle-Vendor')}"
    }
}
task fragmentJar(type: Jar) {

    doFirst {
        //Setting the fragment jar name
        baseName = project.name
        archiveName = "${baseName}.fragment-${version}.jar"

        manifest {
            attributes (
                    'Bundle-Name': 'dotCMS Override fragment',
                    'Bundle-Description': 'dotCMS - Override fragment',
                    'Bundle-Vendor': "${bundleVendor}",
                    'Bundle-Version': "${version}",
                    'Bundle-SymbolicName': "${baseName}.fragment",
                    'Bundle-ManifestVersion': "${bundleManifestVersion}",
                    'Bundle-DocURL': "${bundleDocURL}",
                    'Fragment-Host': 'system.bundle; extension:=framework',
                    'Export-Package': "${importPackage}"
            )
        }
    }
}
fragmentJar.dependsOn 'readManifesttAttributes'

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}