buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:4.0.0'
    }
}


apply plugin: 'java'
apply plugin: 'biz.aQute.bnd.builder'

// Compile Java.
targetCompatibility = 1.8
sourceCompatibility = 1.8

//targetCompatibility = 11
//sourceCompatibility = 11

version = '0.2'

repositories {
    maven { url "http://repo.dotcms.com/artifactory/libs-release" }
}

configurations {
    provided
    provided.extendsFrom(compile)
}

dependencies {
    compile('com.dotcms:dotcms:5.1.5') { transitive = true }

    testCompile 'org.mockito:mockito-core:2.0.31-beta'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile('org.testng:testng:6.9.6') {
        exclude(group: 'org.hamcrest')

    }
}

/* To compile we must add the additional configuration we created to our classpaths. */
sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

test {
    // enable TestNG support (default is JUnit)
    useTestNG()
    /* enabling standard streams allows us to see test failures and related stack traces in the console. */
    testLogging.showStandardStreams = true
}

import java.util.jar.*

/////////////////////////
//Plugin jar
/////////////////////////
jar {
    manifest {
        attributes (
                'Bundle-Vendor': 'dotCMS',
                'Bundle-Description': 'dotCMS - Rules Engine Velocity Scripting Actionlet',
                'Bundle-DocURL': 'https://dotcms.com/',
                'Bundle-Activator': 'com.dotmarketing.osgi.ruleengine.actionlet.Activator',
                'Import-Package': '*;version=0'
        )
    }
}
jar.dependsOn test
//jar.finalizedBy 'fragmentJar'

/////////////////////////
//Fragment jar
/////////////////////////

ext {
    bundleName = "Rules Engine Velocity Scripting Actionlet fragment"
    bundleDescription = "dotCMS - Rules Engine Velocity Scripting Actionlet fragment"
    fragmentHost = "system.bundle; extension:=framework"
    bundleSymbolicName = "" //Auto generated based on the plugin jar
    bundleVersion = "" //Auto generated based on the plugin jar
    importPackage = "" //Auto generated based on the plugin jar
    bundleManifestVersion = "" //Auto generated based on the plugin jar
    bundleDocURL = "" //Auto generated based on the plugin jar
    bundleVendor = "" //Auto generated based on the plugin jar
}
/**
 * The import generates versions like this: version="[1.8,2)"
 * That format does not work for the export, so we need to replace it
 * to: version=0
 */
ext.fixVersionNumber = {importValue ->
    return importValue.replaceAll("\"\\[[0-9.,]+\\)\"", "0")
}

/**
 * Reads the Manifest file of the just created plugin jar in order to get the required info
 * to automatically create the fragment jar.
 */
task readManifesttAttributes {
    doFirst {
        File file = configurations.baseline.singleFile
        JarFile jar = new JarFile(file)
        Attributes manifest = jar.getManifest().getMainAttributes()
        bundleSymbolicName = "${manifest.getValue('Bundle-SymbolicName')}"
        bundleVersion = "${manifest.getValue('Bundle-Version')}"
        importPackage = "${manifest.getValue('Import-Package')}"
        bundleManifestVersion = "${manifest.getValue('Bundle-ManifestVersion')}"
        bundleDocURL = "${manifest.getValue('Bundle-DocURL')}"
        bundleVendor = "${manifest.getValue('Bundle-Vendor')}"
    }
}
task fragmentJar(type: Jar) {

    if (gradle.startParameter.taskNames.contains("fragmentJar")) {

        File file = configurations.baseline.singleFile
        println "file ${file.getPath()}"
        JarFile jar = new JarFile(file)

        def projectName = project.name
        def jarName = "${projectName}.fragment"
        archivesBaseName = jarName

        println "running fragmentJar: archivesBaseName ${archivesBaseName}, archiveFileName ${archiveFileName.get()}"

        doFirst {

            Attributes coreManifest = jar.getManifest().getMainAttributes()
            bundleSymbolicName = "${coreManifest.getValue('Bundle-SymbolicName')}"
            bundleVersion = "${coreManifest.getValue('Bundle-Version')}"
            importPackage = "${coreManifest.getValue('Import-Package')}"
            bundleManifestVersion = "${coreManifest.getValue('Bundle-ManifestVersion')}"
            bundleDocURL = "${coreManifest.getValue('Bundle-DocURL')}"
            bundleVendor = "${coreManifest.getValue('Bundle-Vendor')}"

            //Setting the fragment jar name
            importPackage = fixVersionNumber(importPackage)
            manifest {
                attributes(
                        'Bundle-Name': "${bundleName}",
                        'Bundle-Description': "${bundleDescription}",
                        'Bundle-Vendor': "${bundleVendor}",
                        'Bundle-Version': "${version}",
                        'Bundle-SymbolicName': "${archivesBaseName}",
                        'Bundle-ManifestVersion': "${bundleManifestVersion}",
                        'Bundle-DocURL': "${bundleDocURL}",
                        'Fragment-Host': "${fragmentHost}",
                        'Export-Package': "${importPackage}"
                )
            }
        }
    }
}

//fragmentJar.dependsOn 'readManifestAttributes'
